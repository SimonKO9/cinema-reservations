buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.1.10.RELEASE")
    }
}

defaultTasks 'bower'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'spring-boot'

jar {
    baseName = 'cinema-reservations'
    version =  '1.0.0-SNAPSHOT'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.spring.io/libs-release" }
    maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter:1.2.1.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-log4j:1.2.1.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-web:1.2.1.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-actuator:1.2.1.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-data-jpa:1.2.1.RELEASE")
    compile('org.springframework.boot:spring-boot-starter-security:1.2.1.RELEASE')
    compile("com.h2database:h2")
    testCompile("junit:junit")
}

jar.into('static') {
    from('src/main/webapp')
}

// Get the path for the locally installed binaries
task npmBin << {
    new ByteArrayOutputStream().withStream { os ->
        def result = exec {
            executable = 'npm'
            args = ['bin']
            standardOutput = os
        }
        ext.binPath = os.toString().trim() + "/"
    }
}


// Install packages from package.json
task npm(type: Exec) {
    description = "Grab NodeJS dependencies (from package.json)"
    commandLine = ["npm", "install"]
    inputs.file "package.json"
    outputs.dir "node_modules"

    tasks.npmBin.execute()

}


// Install the bower components for front-end library management
task bower(dependsOn: 'npm', type: Exec){
    commandLine "${npmBin.binPath}bower", 'install'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
